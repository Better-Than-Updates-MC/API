import com.google.gson.JsonArray
import com.google.gson.JsonObject

archivesBaseName = 'beta-config'
version = getSubprojectVersion(project, '1.0.0')

apply plugin: 'java-library'

moduleDependencies(project, 'beta-api-base')

dependencies {
	include api('valoeghese:ZoesteriaConfig:1.3.6')
	include api('blue.endless:jankson:1.2.1')
}

minecraft {
	addIncludeTweaker {dependency, json ->
		if ('Jankson' == dependency.name) {
			json.addProperty('description', 'JSON / HJSON parser and preprocessor which preserves ordering and comments')
			json.addProperty('license', 'MIT')

			def authors = new JsonArray()
			authors.add('falkreon')
			json.add('authors', authors)

			def contributors = new JsonArray()
			contributors.add('Juxxel')
			contributors.add('unascribed')
			contributors.add('NikkyAI')
			json.add('contributors', contributors)

			def contact = new JsonObject()
			contact.addProperty('sources', 'https://github.com/falkreon/Jankson')
			contact.addProperty('issues', 'https://github.com/falkreon/Jankson/issues')
			json.add('contact', contact)
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom.withXml {
				//Workaround from java.components including everything (including the game)
				addDependency(it, 'compile', project.configurations.api.dependencies)
			}
		}
	}
}
